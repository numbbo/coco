@echo off
Rem
Rem The Coco version of octave.bat
Rem
Rem The reasons for asking Coco Windows users to place this file in their
Rem Octave path (the folder above Octave's bin/) for running
Rem Octave have been incompatibilities with the way other operating systems
Rem call Octave. Using this file will not mess up with the Windows
Rem path and thus allows to run for example the Octave and the C example
Rem experiment of Coco at any time.


Rem   Find Octave's install directory through cmd.exe variables.
Rem   This batch file should reside in Octaves installation subdir!
Rem
Rem   This trick finds the location where the batch file resides.
Rem   Note: the result ends with a backslash
set OCT_HOME=%~dp0

Rem   Set up PATH but add Octave home in the end.
set PATH=%OCT_HOME%bin;%PATH%

Rem   Check for args to see if we are told to start GUI
Rem   with the --force-gui option or not (--no-gui)
Rem   Otherwise assume starting as command line
set GUI_MODE=1
:checkargs
if -%1-==-- goto noargs
  if NOT %1==--force-gui goto notguiarg
    set GUI_MODE=1
:notguiarg
  if NOT %1==--no-gui goto notnoguiarg
    set GUI_MODE=0
:notnoguiarg
  shift
  goto  checkargs
:noargs

Rem   Start Octave
if %GUI_MODE%==1 (
  start %OCT_HOME%/bin/octave-gui.exe %*
) else (
  start %OCT_HOME%/bin/octave-cli.exe %*
)
